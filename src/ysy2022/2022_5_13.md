# 2022-5-13

> 初步学习，并使用 `antlr` 编写编译器前端。

按照语言文法，写出初步的 `antlr` 文件

``` antlr
CompUnit    : (CompUnit)?(Decl | FuncDef)
            ;
Decl        : ConstDecl
            | VarDecl
            ;
ConstDecl   : 'const' ('int' | 'float') ConstDef (',' ConstDef)* ';'
            ;
ConstDef    : Ident ('[' ConstExp ']')* '=' ConstInitVal
            ;
ConstInitVal: ConstExp
            | '{' (ConstInitVal (',' ConstInitVal)*)? '}'
            ;
VarDecl     : ('int' | 'float') VarDef (',' VarDef)* ';'
            ;
VarDef      : Ident ('[' ConstExp ']')*
            | Ident ('[' ConstExp ']')* '=' InitVal
            ;
InitVal     : Exp
            | '{' (InitVal (',' InitVal)*)? '}'
            ;
FuncDef     : ('void' | 'int' | 'float') Ident '(' (FuncFParams)? ')' Block
            ;
FuncType    : 'void'
            | 'int'
            | 'float'
            ;
FuncFParams : FuncFParam (',' FuncFParam)*
            ;
FuncFParam  : ('int' | 'float') Ident ('[' ']' ('[' Exp ']')*)?
            ;
Block       : '{' (BlockItem)* '}'
            ;
BlockItem   : Decl
            | Stmt
            ;
Stmt        : LVal '=' Exp ';'
            | (Exp)? ';'
            | Block
            | 'if' '(' Cond ')' Stmt ('else' Stmt)?
            | 'while' '(' Cond ')' Stmt
            | 'break' ';'
            | 'continue' ';'
            | 'return' (Exp)? ';'
            ;
Exp         : AddExp
            ;
Cond        : LOrExp
            ;
LVal        : Ident ('[' Exp ']')*
            ;
PrimaryExp  : '(' Exp ')'
            | LVal
            | Number
            ;
Number      : IntConst
            | FloatConst
            ;
UnaryExp    : PrimaryExp
            | Ident '(' (FuncRParams)? ')'
            | ('+' | '-' | '!') UnaryExp
            ;
UnaryOp     : '+'
            | '-'
            | '!'
            ;
FuncRParams : Exp (',' Exp)*
            ;
MulExp      : UnaryExp
            | MulExp ('*' | '/' | '%') UnaryExp
            ;
AddExp      : MulExp | AddExp ('+' | '-') MulExp
            ;
RelExp      : AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp
            ;
EqExp       : RelExp
            | EqExp ('==' | '!=') RelExp
            ;
LAndExp     : EqExp | LAndExp '&&' EqExp
            ;
LOrExp      : LAndExp
            | LOrExp '||' LAndExp
            ;
ConstExp    : AddExp
            ;
BType       : 'int'
            | 'float'
            ;
Ident       : [a-zA-Z][a-zA-Z0-9]*
            ;
IntConst    : '0' [0-7]*
            | '0x' [0-9a-f]*
            | [1-9][0-9]*
            ;
FloatConst  : [1-9][0-9]*'.'[0-9]*
            ;
WS          : [ \t\r\n]+ -> skip
            ;
```

然后开始将左递归消除，空产生式消除，将关键字，运算符等罗列出。

可是，上面的代码依然不能给出满意的结果。经过对 `.tokens` 文件和 `.interp` 文件的检查，在 StackOverflow 上的[某一个问题](https://stackoverflow.com/questions/29777778/antlr-4-5-mismatched-input-x-expecting-x)了解到，如下可能原因。

`antlr` 约定，小写字母开头的是语法规则，而大写字母开头的是词法规则。

所以再将内容进行一顿修缮，同时增加一个 `s` 转换为增广语法（没什么用，个人习惯）。

写出下列代码，并可以做到满意的效果。

``` antlr
grammar SysY2022;

s           : compUnit EOF
            ;
compUnit    : (decl | funcDef)
            | (decl | funcDef) rCompUnit
            ;
rCompUnit   : (decl | funcDef)
            | (decl | funcDef) rCompUnit
            ;
decl        : constDecl
            | varDecl
            ;
constDecl   : KWconst (KWint | KWfloat) constDef (Comma constDef)* Semicolon
            ;
constDef    : Ident (LSqBracket constExp RSqBracket)* Assign constInitVal
            ;
constInitVal: constExp
            | LCurlyBracket (constInitVal (Comma constInitVal)*)? RCurlyBracket
            ;
varDecl     : (KWint | KWfloat) varDef (Comma varDef)* Semicolon
            ;
varDef      : Ident (LSqBracket constExp RSqBracket)*
            | Ident (LSqBracket constExp RSqBracket)* Assign initVal
            ;
initVal     : exp
            | LCurlyBracket (initVal (Comma initVal)*)? RCurlyBracket
            ;
funcDef     : (KWvoid | KWint | KWfloat ) Ident LBracket (funcFParams)? RBracket block
            ;
funcFParams : funcFParam (Comma funcFParam)*
            ;
funcFParam  : (KWint | KWfloat) Ident (LSqBracket RSqBracket (LSqBracket exp RSqBracket)*)?
            ;
block       : LCurlyBracket (blockItem)* RCurlyBracket
            ;
blockItem   : decl
            | stmt
            ;
stmt        : lVal Assign exp Semicolon
            | (exp)? Semicolon
            | block
            | KWif LBracket cond RBracket stmt (KWelse stmt)?
            | KWwhile LBracket cond RBracket stmt
            | KWbreak Semicolon
            | KWcontinue Semicolon
            | KWreturn (exp)? Semicolon
            ;
exp         : addExp
            ;
cond        : lOrExp
            ;
lVal        : Ident (LSqBracket exp RSqBracket)*
            ;
primaryExp  : LBracket exp RBracket
            | lVal
            | number
            ;
number      : IntConst
            | FloatConst
            ;
unaryExp    : primaryExp
            | Ident LBracket (funcRParams)? RBracket
            | (Plus | Minus | Not) unaryExp
            ;
funcRParams : exp (Comma exp)*
            ;
mulExp      : unaryExp
            | unaryExp rMulExp
            ;
rMulExp     : (Multiply | Divide | Mod) unaryExp
            | (Multiply | Divide | Mod) unaryExp rMulExp
            ;
addExp      : mulExp
            | mulExp rAddExp
            ;
rAddExp     :  (Plus | Minus) mulExp
            |  (Plus | Minus) mulExp rAddExp
            ;
relExp      : addExp
            | addExp rRelExp
            ;
rRelExp     : (Great | Little | GreatEq | LittleEq) addExp
            | (Great | Little | GreatEq | LittleEq) addExp rRelExp
            ;
eqExp       : relExp
            | relExp rEqExp
            ;
rEqExp      : (Equal | Inequal) relExp
            | (Equal | Inequal) relExp rEqExp
            ;
lAndExp     : eqExp
            | eqExp rLAndExp
            ;
rLAndExp    : And eqExp
            | And eqExp rLAndExp
            ;
lOrExp      : lAndExp
            | lAndExp rLOrExp
            ;
rLOrExp     : Or lAndExp
            | Or lAndExp rLOrExp
            ;
constExp    : addExp
            ;

KWint       : 'int'
            ;
KWfloat     : 'float'
            ;
KWvoid      : 'void'
            ;
KWreturn    : 'return'
            ;
KWconst     : 'const'
            ;
KWcontinue  : 'continue'
            ;
KWbreak     : 'break'
            ;
KWif        : 'if'
            ;
KWwhile     : 'while'
            ;
KWelse      : 'else'
            ;
Plus        : '+'
            ;
Minus       : '-'
            ;
Multiply    : '*'
            ;
Divide      : '/'
            ;
And         : '&&'
            ;
Or          : '||'
            ;
GreatEq     : '>='
            ;
LittleEq    : '<='
            ;
Equal       : '=='
            ;
Inequal     : '!='
            ;
Great       : '>'
            ;
Little      : '<'
            ;
Not         : '!'
            ;
Mod         : '%'
            ;
Comma       : ','
            ;
Assign      : '='
            ;
LBracket    : '('
            ;
RBracket    : ')'
            ;
LSqBracket  : '['
            ;
RSqBracket  : ']'
            ;
LCurlyBracket   : '{'
                ;
RCurlyBracket   : '}'
                ;
Semicolon   : ';'
            ;
Ident       : [a-zA-Z_][a-zA-Z0-9_]*
            ;
IntConst    : '0' [0-7]*
            | '0x' [0-9a-f]*
            | [1-9][0-9]*
            ;
FloatConst  : [1-9][0-9]*'.'[0-9]+
            ;
WS          : [ \t\r\n]+ -> skip
            ;
```

说明：

- r开头的语法有如下可能含义
    - right value的右意思，即右值的右。
    - 用于消除左递归而使用的，生成新的右递归。
    - 原本意思里包含r的。
- KW开头的词法都是关键字
- 语法中，使用的词法规则都会额外写一个专用的词法规则。